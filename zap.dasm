; SPACEGAME
; (ZAP.SRC) 
; BY STEVEN HUGG
; FRIDAY, SEP 6, 1985 
;
; converted to DASM on Jun 13, 2020
;
 processor 6502
SHP EQU $A ;SHAPE 
XT EQU $B ;XBIT OFFSET 
XB EQU $6 ;XBYTE 
YP EQU $7 ;YPOS 
SPKR EQU $C030 ;CLICK! 
 ORG $803
Start:
 LDX #0
 STA $C070
PDCKLP INX
 BEQ PDNONE
 LDY #10 
PCKYL DEY
 BNE PCKYL
 LDA $C064
 BMI PDCKLP
PDNONE STX STIK
STARVR JSR CLS ;CLRSCRN 
 LDA $C057 ;HGR 
 LDA $C054 ;PAGE1 
 LDA $C052 ;NOMIX 
 LDA $C050 ;GR 
 LDA $C010
 LDA $C063
 STA RND ;INIT 
 LDA $C062 ;RND & 
 STA RND2 ;RND2 
 LDX #$FD 
 TXS
 LDA #0
 STA $8 ;XPOS 
 STA $9 ;YPOS 
 LDA #<SCORTX
 STA $6
 LDA #>SCORTX
 STA $7
 JSR GPRNT ;PRINT "SCORE:" 
 LDA #30
 STA $8
 LDA #<SHIPSTX
 STA $6
 LDA #>SHIPSTX
 STA $7
 JSR GPRNT
 LDA #3
 STA SHIPS 
 LDY #0 
 LDA #0
LOOP49 STA SCORE,Y ;CLEAR 
 INY  ;SCORE 
 CPY #6
 BNE LOOP49
 JSR PRSCOR
 LDA #1
 STA LEVEL
 LDA #1
 STA BOMBFREQ
 LDA #2 
 STA METRWVS ;WAVES UNTIL METEOR SHOWER
NROUND JSR SHINIT ;SETUP SHIP'S POS 
 JSR LIVSPR ;PRINT LIVES 
 LDA #0
 STA MISSL ;GET RID OF MISSLE 
 STA SHIPDCT ;WAITING TIME
 LDY #7
 LDA #255 
WARSLP STA WALY,Y
 DEY
 BPL WARSLP
;** SETUP MONSTERS
 LDY #0
BISCUIT LDA #1 
 STA ENXT,Y ;XBIT=1 
 LDA #0
 STA ENSTAT,Y
 STA BOMBXB,Y
 STA BOMBXT,Y
 LDA #$FF
 STA BOMBY,Y
 JSR RANDOM
 STA ENDIR,Y ;DIRECT
 JSR RANDOM
 AND #$1F 
 CLC
 ADC #$10
 STA ENQUIK,Y ;SPEED 
 JSR NEWSPD
FISHLP JSR RANDOM
 LSR
 LSR
 LSR
 CMP #18 
 BCS FISHLP
 ASL
 STA ENXB,Y ;0-34 
PURINA JSR RANDOM
 AND #$7F
 CMP #15 
 BCC PURINA
 STA ENY,Y ;0-127 
 LDX METRWVS
 BNE SHPNMTR
 LDA #12 ; SEH??? was #14
 JMP SHPNMOT
SHPNMTR JSR RANDOM
 TAX
 LDA #4
 CPX #60 
 BCS SHPNMOT
 LDA ENQUIK,Y
 ASL
 STA ENQUIK,Y
 LDA #14 ; SEH??? was #20
SHPNMOT STA ENSH,Y
 INY
 CPY #16
 BNE BISCUIT
 LDA LEVEL
 AND #7
 CLC
 ADC #3
 STA MONTOGO ;MONSTERS TO GO
 STA NUMALI ;MONSTERS=LEVEL+3 
MAINLP NOP
 LDA PX1
 STA $6
 LDA PT1
 STA $7
 LDA #4 
 STA $8 ;NUM TO ADD OR SUB 
 LDA P1DIR
 CMP #1
 BEQ YALEFT
NOUSEK JSR PDL
 CPY #$40 ;STICK LEFT? 
 BCS NOLEFT
YALEFT JSR BITSUB ;SUB 2 
 LDA $6
 BPL NORIGHT 
 JSR SETOLD 
 JMP NORIGHT
NOLEFT LDA P1DIR
 CMP #2
 BEQ YARIGHT
 CPY #$C0
 BCC NORIGHT
YARIGHT JSR BITADD ;ADD 2 
 LDA $6
 CMP #37 
 BCC NORIGHT 
 JSR SETOLD 
NORIGHT LDA $6 ;BYTE 
 STA $E0  
 LDA $7 ;BIT 
 STA $E1
; LDA #0 ; SEH???
 LDA #0
NOFLA STA SHP
 LDA $E0
 STA PX1
 LDA $E1
 STA PT1
 CMP #7
 BCC NOGARBG
 LDA #0
 STA PT1
 INC PX1
NOGARBG LDA PX1
 STA XB
 LDA PT1
 STA XT
 LDA #180
 STA YP
 LDA SHIPDCT
 BNE NAHMIE 
 JSR DRAW ;DRAW 
 LDA $C000 ;MOVE BY KEYBOARD
 BPL SDDKDI
 CMP #$92 ;CNTL-R 
 BNE NORSTRT
 JMP STARVR 
NORSTRT CMP #$91 ;CNTL-Q 
 BNE NOQU
 LDA $C051 
 RTS
NOQU CMP #$88 ;<-
 BNE KEDLE
 LDA #1
 STA P1DIR
 LDA $C010
 JMP NAHMIE 
KEDLE CMP #$95
 BNE VOVVV
 LDA #2
 STA P1DIR
 LDA $C010
 JMP NAHMIE
VOVVV CMP #$A0 ;SPC 
 BEQ MISKKB 
 LDA #0
 STA P1DIR
SDDKDI LDA $C061
 BMI MISKPOF ;DOWN
 JMP NAHMIE
MISKKB LDA $C010 
MISKPOF LDA MISSL
 BNE NAHMIE
 LDA #166
 STA MISY
 LDA PX1 ;ADD 
 STA $6 ;1BYTE 
 INC $6 ;AND 
 LDA PT1 ;1BIT 
 STA $7 ;TO 
 LDA #1 ;MISSLE 
 STA $8 ;XPOS 
 JSR BITADD
 LDA $6
 STA MISXB
 LDA $7
 STA MISXT
 LDA #1
 STA MISSL
NAHMIE LDA MISSL ;MISSLE ONSCREEN? 
 BEQ EIMHAN
 LDA MISY
 STA YP
 LDA MISXB
 STA XB
 LDA MISXT
 STA XT
 LDA #2
 STA SHP
 JSR DRAW
 LDX MISY
 LDY #4  
 JSR SOUND ;BEEP 
 LDA MISY
 SEC
 SBC #5 
 STA MISY
 CMP #13 ;OVER TOP?
 BCS EIMHAN
 LDA MISY
 STA YP
 JSR BLNKUS
 JSR DRAW
 JSR UNBLNK
 LDA #0
 STA MISSL ;DEACTIVATE 
EIMHAN NOP
 LDY #0
MONSTLP LDA ENSTAT,Y ;STATUS 
 AND #$40
 BEQ NOBLOWN
 JMP SKIP66
NOBLOWN NOP
 LDA ENXB,Y
 STA XB
 LDA ENXT,Y
 STA XT
 LDA ENY,Y
 STA YP
 LDA ENSH,Y
 STA $E2
 LDA ENSTAT,Y
 BMI NOBONG ;ZAPPED 
 LDX ENSH,Y 
 CPX #14
 BEQ NOBONG
 LDA $E2 
 JMP FRENCH
NOBONG AND #2
 CLC
 ADC $E2 ;ADD BIT 2 TO SHP 
FRENCH STA SHP
 STY $E2
 JSR DRAW
 LDY $E2
 LDA MISSL
 BEQ NOENHIT
 LDA ENSTAT,Y
 BMI NOENHIT
 LDA ENY,Y
 CLC
 ADC #4 
 CMP MISY 
 BCS NOENHIT ;Y+4<MY 
 CLC
 ADC #10 
 CMP MISY
 BCC NOENHIT ;Y+10<MY 
 LDA ENXB,Y
 LDX ENSH,Y 
 CPX #14
 BNE JIEOEO
 SEC
 SBC #1
JIEOEO CMP MISXB
 BCS NOENHIT
 CLC
 ADC #3 
 CMP MISXB
 BCC NOENHIT ;X NOT RIGHT, SO NO HIT 
 LDA ENSH,Y 
 CMP #14
 BNE KABOOM ;NO METR
 JMP KABOOM
NOENHIT JMP NHITEN
KABOOM LDA ENSH,Y
 PHA
 LDA #6 ;BLOW HIM UP!!!!!! 
 STA ENSH,Y
 LDA ENSTAT,Y
 ORA #$80 ;HIB-ON 
 STA ENSTAT,Y
 LDA #0
 STA MISSL ;MISOFF 
 JSR BLNKUS
 LDA ENY,Y
 STA YP
 LDA ENXB,Y
 STA XB
 LDA ENXT,Y
 STA XT
 STY $DF
 JSR DRAW
 JSR UNBLNK
 JMP IJERHG
NHITEN JMP NOENHIT2
IJERHG LDA #5 
 STA $E3
 LDY $DF
 LDA ENY,Y
 CLC
 ADC #6
 STA YP
 LDA ENXB,Y
 STA XB
 LDA ENXT,Y
 STA XT
 LDA #10
 STA SHP
 JSR INVRUS
 JSR DRAW
 JSR UNBLNK
ZNFRLP JSR RANDOM
 TAX
 LDY #4 
 JSR SOUND
 DEC $E3
 BNE ZNFRLP
 JSR MISERAS
 JSR SADCLR
 PLA
 TAX
 LDA #8 
 CPX #14
 BNE NOMRMRT
 LDA #1
 STA SCORAD+3 
 LDA #5
NOMRMRT STA SCORAD+4
 JSR SCORADD
 LDY $E2
NOENHIT2 NOP
 LDA ENSTAT,Y
 EOR #2 ;FLAME 
 STA ENSTAT,Y
NOFALL NOP
SKIP66 INY
 CPY NUMALI
 BEQ TJDL 
 JMP MONSTLP
TJDL NOP
 LDY #0
BOMBLP LDA BOMBY,Y
 CMP #192
 BCS BOMBGON
 STA YP
 LDX ENSH,Y
 CPX #14 
 BNE OKNFOR
 JMP QWEQUA 
OKNFOR LDA BOMBXB,Y
 STA XB
 LDA BOMBXT,Y
 STA XT
 LDA #10
 STA SHP 
 STY $E2
 JSR DRAW
 LDY $E2
 LDA BOMBY,Y
 CLC
 ADC #5
 STA BOMBY,Y
 JMP QWEQUA
BOMBGON LDA ENSH,Y
 CMP #20
 BEQ BOMBIT
 JSR RANDOM
 CMP BOMBFREQ
 BCS QWEQUA 
 LDA SHIPDCT ;SHIP GONE? 
 BNE QWEQUA
 LDA ENSTAT,Y
 AND #$C0
 BNE QWEQUA
BOMBIT LDA ENXB,Y
 STA BOMBXB,Y
 LDA ENXT,Y
 STA BOMBXT,Y
 LDA ENY,Y
 CLC
 ADC #10
 STA BOMBY,Y
QWEQUA INY
 CPY NUMALI 
 BNE BOMBLP
;*** DO WALLS 
 LDY #0
WALLP LDA WALY,Y 
 CMP #192
 BCS NOWDR
 BEQ NOWDR
 LDA WALY,Y 
 STA YP
 LDA WALXB,Y 
 STA XB
 LDA WALXT,Y 
 STA XT
 LDA #22
 STA SHP
 STY $E2
 JSR DRAW
 LDX #1
 LDY #1
 JSR SOUND
 LDY $E2
 LDA WALY,Y 
 CLC
 ADC WALDIR,Y 
 STA WALY,Y 
 JMP MORWALL
NOWDR JSR RANDOM 
 TAX 
 LDA LEVEL
 ASL
 STA $E2
 CPX $E2
 BCS MORWALL
 JSR NEWWALL
MORWALL INY 
 CPY #8
 BNE WALLP
;** MOVE MONSTERS
MOVMTRS LDY #0
MONMVLP STY $E2
 LDA ENSTAT,Y
 AND #$40 ;BIT THE DUST? 
 BEQ NOKCCO
 JMP COOKIE
NOKCCO LDA ENSTAT,Y ;FALL? 
 BPL CHUCHU ;NO. 
 LDA ENY,Y
 CLC
 ADC #6
 STA ENY,Y
 CMP #200
 BCS ZZARF2 
 LDX #200
 LDY #6
 JSR SOUND
 JMP COOKIE
ZZARF2 LDA #$40
 STA ENSTAT,Y ;DEAD 
 DEC MONTOGO ;SCRATCH ONE MONSTER 
 JMP COOKIE 
CHUCHU LDA ENSH,Y ;SHOWR?
 CMP #14
 BNE NEINDN ;NO.
 LDX ENYMV,Y 
 LDA METMOV,X 
 CLC
 ADC ENY,Y
 STA ENY,Y
 CMP #230
 BCC NVRVRV
JAMACIA JSR RANDOM
 AND #$1E
 EOR #1
 CMP #39
 BCS JAMACIA
 STA ENXB,Y
NVRVRV LDA ENY,Y
 LDY #3
 JSR SOUND
 JMP COOKIE
NEINDN LDA ENDIR,Y
 AND #1
 BNE INKO
 LDA #$FE ;0-UP 
INKO CLC
 ADC ENYMV,Y
 CLC
 ADC ENY,Y
 CMP #128
 BCS HALTZZ 
 CMP #14 
 BCS ZZARF
HALTZZ LDA ENDIR,Y
 EOR #1
 STA ENDIR,Y
 LDA ENY,Y
ZZARF STA ENY,Y
 LDA ENXB,Y
 STA $6
 LDA ENXT,Y
 STA $7
 LDA ENXMV,Y
 STA $8 
 LDA ENDIR,Y
 AND #2
 BNE MOGOWA 
 JSR BITSUB ;0-UP 
 JMP MASH
MOGOWA JSR BITADD
MASH LDA $6 
 CMP #36 
 BCC TOAST
 LDA ENDIR,Y
 EOR #2
 STA ENDIR,Y
 JSR OLDMON 
TOAST LDA $6
 STA ENXB,Y
 LDA $7
 STA ENXT,Y
 JSR RANDOM
 CMP ENQUIK,Y ;MOVE AROUND 
 BCS COOKIE ;RND<ENQUIK, CHANGE DIR
 JSR RANDOM
 STA ENDIR,Y
 JSR NEWSPD
COOKIE LDY $E2
 INY
 CPY NUMALI
 BEQ TJDL2 
 JMP MONMVLP
TJDL2 NOP
;SING-STEP
 LDA $C000
 CMP #$9B
 BNE NNESC
 LDA $C010
ESCLP LDA $C000
 BPL ESCLP
 CMP #$9B
 BEQ NNESC ;IF ESC, DON'T CLEAR KEYBOARD 
 LDA $C010
NNESC LDA $C000 
 CMP #$93 ;CNTL-S 
 BNE NNCS
 LDA SNDCLIK+1 
 EOR #$30 
 STA SNDCLIK+1 
 STA SNDCLK2+1
 LDA $C010
NNCS NOP
 LDA MONTOGO
 BNE STILLPLAY
 JMP NEWLEVEL
STILLPLAY EQU * 
 LDA SHIPDCT
 BNE NOTSRY
 LDX #179 
 LDA LOOKL,X
 STA $27
 LDA LOOKH,X
 STA $28
 LDA #0
 STA $E2
 LDY PX1
 INY
 LDX #2
MFOSCI LDA ($27),Y
 ORA $E2
 STA $E2
 INY
 DEX
 BNE MFOSCI
 LDA $E2
 AND #$7F
 BEQ NOTSRY
 JSR EXPLODE
 JSR NEWSHIP
NOTSRY EQU *
 LDA SHIPDCT
 BEQ DONTIME
 DEC SHIPDCT
 JSR SHINIT
DONTIME EQU *
 LDA METRWVS
 BEQ NOSTRMR
 JSR RANDOM
 CMP LEVEL 
 BCS NOSTRMR
 LDA ENSTAT 
 AND #$40 ;DEAD? 
 BEQ NOSTRMR
 LDA ENSH 
 CMP #14 ;ALREADY METEOR? 
 BEQ NOSTRMR
 INC MONTOGO
 LDA #14
 STA ENSH ;PUT A STRAY METEOR
 LDA #0
 STA ENY 
 STA ENSTAT
NOSTRMR EQU *
 JMP MAINLP
NEWLEVEL JSR CLS2 
 INC LEVEL
 LDA BOMBFREQ
 CLC
 ADC #4
 STA BOMBFREQ
 JSR CLS2
 LDA #15
 STA $8
 LDA #90
 STA $9
 LDA #<LEVLTX
 STA $6
 LDA #>LEVLTX
 STA $7
 JSR GPRNT
 LDA LEVEL
 JSR B2D
 DEC METRWVS 
 BPL WOZHMV
 LDA #3
 STA METRWVS
WOZHMV LDA METRWVS
 BNE NLNOSHWR
 LDA #12
 STA $8
 LDA #100
 STA $9
 LDA #<METRTX
 STA $6
 LDA #>METRTX
 STA $7
 JSR GPRNT
NLNOSHWR LDA #0
 STA $DD
 STA $DE
KPWDLP LDA $DD
 STA XB
 LDA $DE
 STA XT
 LDA #40
 STA YP
 LDA #12
 STA SHP
 JSR DRAW
 LDA $DD
 STA $6
 LDA $DE
 STA $7
 LDA #2
 STA $8
 JSR BITADD
 JSR RANDOM
 TAX
 LDY #4
 JSR SOUND
 LDA $DE
 ASL
 ASL
 ASL
 TAX
 LDY #5
 JSR SOUND
 LDA $DD 
 ASL
 ASL
 ASL
 ASL
 TAX
 LDY #5
 JSR SOUND
 LDA $7
 STA $DE
 LDA $6
 STA $DD
 CMP #38 
 BNE KPWDLP
 JSR CLS2
 JMP NROUND
NEWWALL JSR RANDOM
 LSR
 CMP #40
 BCS NEWWALL
 STA WALXB,Y
 AND #1
 EOR #1
 STA $0 
NEWWXT JSR RANDOM
 AND #6
 ORA $0
 CMP #7
 BEQ NEWWXT
 STA WALXT,Y
 LDA #1
 STA WALY,Y
 JSR RANDOM
 AND #3
 CLC
 ADC #1
 STA WALDIR,Y
 RTS
NEWSHIP JSR CLS2
 DEC SHIPS
 JSR LIVSPR
 LDA #50
 STA SHIPDCT
 LDA SHIPS
 BEQ AENDS 
 JMP NOEND
AENDS LDA #10
 STA $9
GMOVRLP LDA #14
 STA $8
 LDA #<GMOVTX
 STA $6
 LDA #>GMOVTX
 STA $7
 JSR GPRNT
 LDX $9
 LDY #10 
 JSR SOUND
 LDX #180
 LDY #8 
 JSR SOUND
 LDA $9
 CLC
 ADC #10
 STA $9
 CMP #101 
 BCC GMOVRLP
 LDA #10
 STA $9
GMOVRER LDA #14
 STA $8
 LDX #10
 JSR SPACES
 LDX $9
 LDY #6 
 JSR SOUND
 LDX #80
 LDY #10 
 JSR SOUND
 LDA $9
 CLC
 ADC #10
 STA $9
 CMP #91 
 BCC GMOVRER
 LDA $C010
ENDDDD LDA $C000 
 BPL ENDDDD 
 JMP STARVR
NOEND EQU *
 RTS 
DCYC LDX #0
 LDY #40 
DCYC1 DEX
 BNE DCYC1
 DEY
 BNE DCYC1
 RTS
SHINIT LDA #18
 STA PX1
 LDA #1
 STA PT1
 LDA #0
 STA P1DIR
 RTS
LIVSPR LDA #0
 STA $9
 LDA #37
 STA $8
 LDA SHIPS
 CLC
 ADC #$10
 JSR GCOUT
 RTS
EXPLODE LDY #0
EXSETUP LDA PX1 ;PLAYER X 
 CLC 
 ADC #1
 STA EXPXB,Y
 LDA PT1 ;PLAYER X BIT 
 STA EXPXT,Y
 LDA #0
 STA EXFRX,Y ;INIT 
 STA EXFRY,Y ;FRACTIONS 
 STA EXPLO,Y
 LDA #185
 STA EXPY,Y ;Y POS 
 JSR RANDOM
 STA EXIX,Y
 JSR RANDOM
 STA EXIY,Y
 JSR RANDOM
 AND #1
 STA EXDX,Y ;X DIR: 0 LEFT, 1 RIGHT 
 INY
 CPY #16
 BNE EXSETUP
 LDA #70 
 STA EXMORE
EXMAIN LDY #0
EXPLP STY $E3 ;PUT IN TEMP
 LDX EXPY,Y 
 LDY #2
 JSR SOUND
 LDY $E3 ;GET YREG BACK 
 LDA EXPLO,Y
 BNE NOPLNOW ;IF EXPL=0, PLOT DOT
 LDA EXPY,Y ;DOTY 
 STA $E2 ;PUT IT AWAY 
 LDA EXPXT,Y ;DOTBIT 
 LDX EXPXB,Y ;DOTBYT 
 LDY $E2 ;GET YPOS BACK 
 JSR PLOT 
 LDY $E3 ;RESTORE Y 
NOPLNOW LDA #1
 STA EXPLO,Y
 LDA EXFRX,Y ;GET X FRACTION
 CLC
 ADC EXIX,Y ;ADD INCREMENT
 STA EXFRX,Y
 BCC NOXNI ;DON'T MOVE DOT XPOS 
 LDA EXPXB,Y
 STA $6
 LDA EXPXT,Y
 STA $7
 LDA #1 
 STA $8
 LDA EXDX,Y
 BEQ DOTLFT ;IT'S ZERO. MOVE LEFT. 
 JSR BITADD
 JMP AFXMV
DOTLFT JSR BITSUB
AFXMV LDA $6
 STA EXPXB,Y ;PUT 
 LDA $7 ;X POSITIONS 
 STA EXPXT,Y ;BACK 
 LDA #0 
 STA EXPLO,Y ;ON NEXT LOOP, PLOT 
NOXNI LDA EXFRY,Y
 CLC
 ADC EXIY,Y
 STA EXFRY,Y
 BCC NOYNI
 LDA EXPY,Y
 SEC 
 SBC #1
 STA EXPY,Y
 LDA #0
 STA EXPLO,Y ;PLOT ON NEXT LOOP 
NOYNI INY
 STY $E3
 JSR RANDOM
 TAX
 LDY #2
 JSR SOUND
 LDY $E3
 CPY #16
 BEQ EXPLPDN 
 JMP EXPLP
EXPLPDN EQU *
 LDX #190 
DELAY11 DEX
 BNE DELAY11 ;WASTE TIME 
 DEC EXMORE ;DONE IT (EXMORE) TIMES? 
 BEQ NNMDOT ;YES. RETURN. 
 JMP EXMAIN
NNMDOT RTS
EXPXB DS 16
EXPXT DS 16
EXPY DS 16
EXFRX DS 16
EXFRY DS 16
EXIX DS 16
EXIY DS 16
EXDX DS 16
EXPLO DS 16
EXMORE DS 1 
;**************
; SUBROUTINES *
;**************
GCOUT AND #$7F
 LDX #8
 STX $F9
 LDX #>CHARSET 
 STX $1B
 ASL
 ASL
 BCC GHJ2
 INC $1B
GHJ2 ASL
 BCC GHJ3
 INC $1B
GHJ3 STA $1A
 LDX $9
GCOLP LDA LOOKL,X
 STA $27
 LDA LOOKH,X
 STA $28
 LDY #0
 LDA ($1A),Y
 LDY $8
 STA ($27),Y
 INX
 DEC $F9 
 BNE ANOLN
 INC $8
 RTS
ANOLN INC $1A 
 BNE GCOLP
 INC $1B
 JMP GCOLP
GPRADR STA $6 
 STY $7
GPRNT LDY #0
GPRNLP LDA ($6),Y
 CMP #$FF
 BEQ GPRTS
 STY $E4
 JSR GCOUT
 LDY $E4
 INY
 BNE GPRNLP
 INC $7
 JMP GPRNLP
GPRTS RTS
SPACES STX $FD ;NUMBER IN X 
 LDA #0
 JSR GCOUT 
 LDX $FD
 DEX
 BNE SPACES 
 RTS
BLNKUS LDA #$A9
 STA GBLANK
 LDA #0
 STA GBLANK+1
 RTS 
INVRUS LDA #$A9
 STA GBLANK
 LDA #$7F
 STA GBLANK+1
 RTS
UNBLNK LDA #$B1
 STA GBLANK
 LDA #$1C
 STA GBLANK+1
 RTS
B2D LDX #0
 STX $EE
C100 CMP #100
 BCC CCOUT1
 SBC #100
 INX
 JMP C100
CCOUT1 JSR CPUTOUT
 LDX #0
C10 CMP #10
 BCC CCOUT2
 SBC #10
 INX
 JMP C10
CCOUT2 JSR CPUTOUT
 CLC
 ADC #$10 
 JMP GCOUT
CPUTOUT PHA 
 TXA 
 LDX $EE
 BNE CPUTTT
 CMP #0
 BNE CPUTTT
 PLA
 RTS
CPUTTT LDX #1
 STX $EE
 CLC
 ADC #$10 
 JSR GCOUT
 PLA
 RTS
;PLOT-XB:X XT:A Y:Y
PLOT PHA
 CMP #40
 BCS PLOTNIE
 CPY #192
 BCS PLOTNIE
 CPY #8
 BCC PLOTNIE
 LDA LOOKL,Y
 STA $27
 LDA LOOKH,Y
 STA $28
 TXA
 TAY
 PLA
 TAX 
 LDA DOTTABL,X 
 EOR ($27),Y
 STA ($27),Y
 RTS
PLOTNIE PLA 
 RTS
OLDMON LDA ENXB,Y
 STA $6
 LDA ENXT,Y
 STA $7
 RTS
SETOLD LDA PX1
 STA $6
 LDA PT1
 STA $7
 RTS
NEWSPD JSR RANDOM
 AND #1
 CLC
 ADC #1
 ASL
 STA ENXMV,Y ;XMV-2,4 
 JSR RANDOM
 AND #1
 STA ENYMV,Y ;YMV-0,1 
 RTS
SOUND STX $0 ;PITCH 
SNDLP2 LDX $0
SNDCLIK LDA SPKR
SNDLP1 DEX
 BNE SNDLP1
SNDCLK2 LDA SPKR
 DEY  ;DUR 
 BNE SNDLP2
 RTS
PDL LDA STIK
 BEQ PDLXE 
 LDX #0
 STA $C070
PDLOOP INX
 BEQ PDLEX 
 LDA $C064
 BMI PDLOOP
PDLEX DEX
 TXA
 TAY
PDLWT LDA $2000,Y 
 LDA $2000,X
 INX 
 BNE PDLWT
 TYA
 CLC
 ADC RND
 STA RND
 RTS
PDLXE LDY #127
 RTS
RANDOM LDA RND2 
 ADC RND
 ROR
 ROR
 STA RND2 
 ROL
 ROL
 SEC
 SBC $C063 
 ROL
 STA RND
 RTS
; BITADD-ADD BIT TO BYTE
; BYTE IN $6
; BIT IN $7
; ADD-BIT IN $8
BITADD LDA $7
 CLC
 ADC $8
 CMP #7
 BCC BANOK
 SEC
 SBC #7
 INC $6
BANOK STA $7 
 RTS
BITSUB LDA $7
 SEC 
 SBC $8
 BPL BANOS
 CLC
 ADC #7
 DEC $6
BANOS STA $7 
 RTS
MISERAS LDA MISXB
 STA XB
 LDA MISXT
 STA XT
 LDA MISY
 STA YP 
 LDA #2
 STA SHP
 JSR BLNKUS
 JSR DRAW
 JSR UNBLNK
 RTS
SADCLR LDY #0
 LDA #0
SRLCDA STA SCORAD,Y
 INY
 CPY #6
 BNE SRLCDA
 RTS
FCARRY .byte $0
SCORADD LDY #0 
 STY FCARRY
 LDY #5 
SCADL LDA SCORAD,Y
 CLC
 ADC FCARRY
 CLC
 ADC SCORE,Y
 STA SCORE,Y
 CMP #10
 BCC DEBAS
 SEC
 SBC #10
 STA SCORE,Y
 LDA #1 
 STA FCARRY
 JMP DEBAS2
DEBAS LDA #0 
 STA FCARRY
DEBAS2 DEY 
 CPY #$FF 
 BNE SCADL
 JMP PRSCOR
PRSCOR LDY #0
 STY $D8
 LDA #7
 STA $8
 LDA #0
 STA $9
PRSRP LDA SCORE,Y
 BNE SCNOT0
 CPY #5
 BEQ SCNOT0
 LDX $D8
 BNE SCNOT0 
 LDA #0
 JMP SCPCSK
SCNOT0 CLC
 ADC #$10 ;MAKE A NUMBER
 LDX #1
 STX $D8
 STY $E7 
 JSR GCOUT
 LDY $E7 
SCPCSK INY
 CPY #6
 BNE PRSRP
 LDX #6
 JSR SPACES
 RTS
CLS LDA #0
 STA $6
 LDA #32
 STA $7
 STA $8
 LDY #0
 LDA #0
CLOOP STA ($6),Y
 INY
 BNE CLOOP
 INC $7
 DEC $8
 BNE CLOOP
 RTS
CLS2 LDX #8
CLOOP4 LDA LOOKL,X
 STA $27
 LDA LOOKH,X
 STA $28
 LDA #0
 LDY #0
CLOOP3 STA ($27),Y
 INY
 CPY #40
 BNE CLOOP3
 INX
 CPX #192
 BNE CLOOP4
 RTS
;** MAGE-PLOT
; PLOTS SHAPE
; FOR SHAPE AT $A 
; BIT*2 IN $B 
; BYTE IN $6
; YPOS IN $7
;
DRAW LDY SHP 
 CPY #22 ; SEH: why outa range?
 BCC InBounds
 RTS
InBounds
 LDA $4000,Y
 STA $1A
 LDA $4001,Y
 STA $1B
 LDA XT 
 ASL ;BIT*2
 TAY
 INY
 LDA ($1A),Y
 CLC
 ADC $1A 
 STA $1C 
 DEY 
 LDA ($1A),Y
 ADC $1B 
 STA $1D 
 LDY #14
 LDA ($1A),Y
 STA WIDTH
 INY
 LDA ($1A),Y
 STA HEIGHT
 LDY #0
LOOP2 LDA WIDTH
 STA WIDTH2
 LDA #0
 STA $9
 LDX $7
 LDA LOOKL,X
 CLC
 ADC $6
 STA $27
 LDA LOOKH,X
 STA $28
LOOP1 LDA $9
 CMP #40
 BCS SKIPLT
 LDA $7 ;Y
 CMP #10
 BCC SKIPLT
 CMP #192
 BCS SKIPLT
;GBLANK: $A9,$0 IS BLANK, $B1,$1C IS NORM
GBLANK LDA ($1C),Y
 STY $8
 LDY $9 
;GMODE: $51 IS EOR, $91 IS STA
GMODE STA ($27),Y
 STA ($27),Y
 LDY $8
SKIPLT INY
 INC $9
 DEC WIDTH2
 BNE LOOP1
SKIPLN INC $7
 DEC HEIGHT
 BNE LOOP2
 RTS
WIDTH .byte $0 
WIDTH2 .byte $0 
HEIGHT .byte $0 
;
RND .byte $0
RND2 .byte $0
PX1 .byte $0 ;XBYT 
PT1 .byte $0 ;XBIT 
MISSL .byte $0
MISY .byte $0
MISXB .byte $0
MISXT .byte $0
NUMALI .byte $0 
MONTOGO .byte $0
LEVEL .byte $1
BOMBFREQ .byte $1
DOTTABL .byte $1,$2,$4,$8,$10,$20,$40
SCORTX .byte $53,$63,$6F,$72,$65,$1A,$FF ;"SCORE:" 
SHIPSTX .byte $53,$68,$69,$70,$73,$1A,$FF ;"SHIPS:"
GMOVTX .byte $47,$41,$4D,$45,$0,$0,$4F,$56,$45,$52,$FF ;"GAME  OVER" 
LEVLTX .byte $4C,$45,$56,$45,$4C,$0,$0,$FF
METRTX .byte $4D,$65,$74,$65,$6F,$72,$0,$0,$73,$68,$6F,$77,$65,$72,$FF ;"METEOR  SHOWER"
METMOV .byte $4,$6
SCORE DS 6 
SCORAD DS 6 
ENXB DS 16 
ENXT DS 16 
ENY DS 16 
ENSH DS 16 
ENDIR DS 16 
ENQUIK DS 16 
ENXMV DS 16 
ENYMV DS 16 
ENSTAT DS 16 
BOMBY DS 16 
BOMBXB DS 16 
BOMBXT DS 16 
SHIPS DS 1
SHIPDCT DS 1
P1DIR DS 1
METRWVS DS 1
WALXB DS 8
WALXT DS 8
WALY DS 8
WALDIR DS 8
STIK .byte $0 ; joystick present?

; hi-res lookup tables
 align $100
LOOKL
; .byte $00,$00,$1e,$80,$01
 .byte $00,$00,$00,$00,$00,$00,$00,$00,$80,$80,$80,$80
 .byte $80,$80,$80,$80,$00,$00,$00,$00,$00,$00,$00,$00,$80,$80,$80,$80
 .byte $80,$80,$80,$80,$00,$00,$00,$00,$00,$00,$00,$00,$80,$80,$80,$80
 .byte $80,$80,$80,$80,$00,$00,$00,$00,$00,$00,$00,$00,$80,$80,$80,$80
 .byte $80,$80,$80,$80,$28,$28,$28,$28,$28,$28,$28,$28,$a8,$a8,$a8,$a8
 .byte $a8,$a8,$a8,$a8,$28,$28,$28,$28,$28,$28,$28,$28,$a8,$a8,$a8,$a8
 .byte $a8,$a8,$a8,$a8,$28,$28,$28,$28,$28,$28,$28,$28,$a8,$a8,$a8,$a8
 .byte $a8,$a8,$a8,$a8,$28,$28,$28,$28,$28,$28,$28,$28,$a8,$a8,$a8,$a8
 .byte $a8,$a8,$a8,$a8,$50,$50,$50,$50,$50,$50,$50,$50,$d0,$d0,$d0,$d0
 .byte $d0,$d0,$d0,$d0,$50,$50,$50,$50,$50,$50,$50,$50,$d0,$d0,$d0,$d0
 .byte $d0,$d0,$d0,$d0,$50,$50,$50,$50,$50,$50,$50,$50,$d0,$d0,$d0,$d0
 .byte $d0,$d0,$d0,$d0,$50,$50,$50,$50,$50,$50,$50,$50,$d0,$d0,$d0,$d0
 .byte $d0,$d0,$d0,$d0
 align $100
LOOKH
 .byte $20,$24,$28,$2c,$30,$34,$38,$3c,$20,$24,$28,$2c
 .byte $30,$34,$38,$3c,$21,$25,$29,$2d,$31,$35,$39,$3d,$21,$25,$29,$2d
 .byte $31,$35,$39,$3d,$22,$26,$2a,$2e,$32,$36,$3a,$3e,$22,$26,$2a,$2e
 .byte $32,$36,$3a,$3e,$23,$27,$2b,$2f,$33,$37,$3b,$3f,$23,$27,$2b,$2f
 .byte $33,$37,$3b,$3f,$20,$24,$28,$2c,$30,$34,$38,$3c,$20,$24,$28,$2c
 .byte $30,$34,$38,$3c,$21,$25,$29,$2d,$31,$35,$39,$3d,$21,$25,$29,$2d
 .byte $31,$35,$39,$3d,$22,$26,$2a,$2e,$32,$36,$3a,$3e,$22,$26,$2a,$2e
 .byte $32,$36,$3a,$3e,$23,$27,$2b,$2f,$33,$37,$3b,$3f,$23,$27,$2b,$2f
 .byte $33,$37,$3b,$3f,$20,$24,$28,$2c,$30,$34,$38,$3c,$20,$24,$28,$2c
 .byte $30,$34,$38,$3c,$21,$25,$29,$2d,$31,$35,$39,$3d,$21,$25,$29,$2d
 .byte $31,$35,$39,$3d,$22,$26,$2a,$2e,$32,$36,$3a,$3e,$22,$26,$2a,$2e
 .byte $32,$36,$3a,$3e,$23,$27,$2b,$2f,$33,$37,$3b,$3f,$23,$27,$2b,$2f
 .byte $33,$37,$3b,$3f
 align $100
CHARSET
; hex 001B0003
 hex 000000000000000018181818
 hex 180018003C3C3C000000000078BCFE3C
 hex FF9E1E00187C1E3C783E18000E6E3018
 hex 0C7670000C1E1E0C7E367C0018181800
 hex 00000000180C0606060C180018306060
 hex 60301800187E3C183C7E18000018187E
 hex 1818000000000000009C988C0000007E
 hex 000000000000000000009C0000603018
 hex 0C0600009CB6666666B69C00181C1818
 hex 18183C003C6660308C867E007EB03038
 hex 60663C0030383C367E3030007E063E60
 hex 60663C00380C863E66663C007EB03098
 hex 188C0C003C66663C66663C003C66667C
 hex B0301C0000009C00009C000000009C00
 hex 009C988C30180C060C18300000007E00
 hex 7E0000000C18306030180C003C66B098
 hex 180018003C667E7E3E067C00189C3CB6
 hex 7E6666003E66663E66663E003C660606
 hex 06663C003E66666666663E007E06063E
 hex 06067E007E06063E060606007C060606
 hex 76667C006666667E666666003C181818
 hex 18183C006060606060663C0066361E0E
 hex 1E3666000606060606067E00667E7E7E
 hex 6666660066666E7E766666003C666666
 hex 66663C003E66663E060606003C666666
 hex 7E367C003E66663E1E3666003C66063C
 hex 60663C007E1818181818180066666666
 hex 66663C00666666B63C9C18006666667E
 hex 7E7E660066B63C183CB6660066B63C18
 hex 181818007EB030180C867E007E0E0E0E
 hex 0E0E7E0000060C18306000007E707070
 hex 70707E000000183C6600000000000000
 hex 000000FF0C1830000000000000003C60
 hex 7C667C0006063E6666663E0000007C06
 hex 06067C0060607C6666667C0000003C66
 hex 7E067C00386C0C3E0C0C0C0000003C66
 hex 667C603C06063E666666660018001C18
 hex 18183C00300038303030361C06066636
 hex 1E3666001C18181818183C0000007E7E
 hex 7E7E660000003E666666660000003C66
 hex 66663C0000003E66663E060600007C66
 hex 667C606000007E0E0606060000007C06
 hex 3C603E000C0C3E0C0C6C380000006666
 hex 66767C00000066B63C9C180000006666
 hex 7E7E7E000000663C183C6600000066B6
 hex 3C9C188E00007E30180C7E00781C1C0E
 hex 1C1C780018181818181818181E383870
 hex 38381E007C3E000000000000FFFFFFFF
 hex FFFFFFFF

 org $2000
HGR1:
 .byte 0
 org $4000 - 4 ; make room for header
Sprites:
 incbin "game.stb"
